plugins {
    id 'java'
}

defaultTasks 'clean', 'test'

group 'org.appium.starterkit'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "io.appium:java-client:${appiumJavaClientVersion}"
    implementation "org.seleniumhq.selenium:selenium-java:${seleniumJavaVersion}"
    implementation "com.aventstack:extentreports:${extentReportVersion}"
    implementation "org.testng:testng:${testNGVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    testImplementation "org.assertj:assertj-core:${assertjCoreVersion}"
}

test {

    useTestNG() {

        listeners << 'com.tw.testUtilities.listeners.TestNGListener'
        testLogging.showStandardStreams = true

        if (project.hasProperty('groups')) {
            includeGroups((String) project.findProperty('groups'))
            excludeGroups 'flaky', 'oos'
        }

        options {
            setParallel('methods')
            setThreadCount(1)
        }

        outputDirectory = file("$buildDir/testng-output")

        suites('suite-runner/testng.xml')

    }

    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }

}